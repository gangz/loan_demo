*** Settings ***
Documentation     - 主贷人的年龄加上住房贷款的年限，男性不能超过65，女性不能超过60。
...               - 住房贷款的期限最长为30年。
...               - 贷款年限加上房龄的总和不能超过40 年。
Test Template     贷款年限审查流程
Resource          resource.txt

*** Test Cases ***    性别            年龄           房龄    贷款年限
贷款年限规则                [Tags]        completed
                      [Setup]       打开首页
                      男             60           0     6
                      男             64           0     1
                      男             65           0     0
                      男             67           0     0       #超龄时贷款年限为0
                      男             20           0     30
                      男             20           30    10      #房龄+时长<=40
                      女             60           0     0
                      女             61           0     0
                      女             59           0     1
                      [Teardown]    关闭当前网页

*** Keywords ***
贷款年限审查流程
    [Arguments]    ${gender}    ${age}    ${house_age}    ${max_period}
    设定主贷人性别    ${gender}
    设定主贷人年龄    ${age}
    设定房龄    ${house_age}
    提交审核
    最大贷款年限应该是    ${max_period}
